Properties liquibaseProperties = new Properties()
liquibaseProperties.load(new FileInputStream(
        file("src/main/resources/db/database.properties").exists() ? file("src/main/resources/db/database.properties") : file("src/main/resources/db/database.properties.sample")))


buildscript {
    ext {
        springBootVersion = '2.7.17'
        liquibaseVersion = '2.0.4'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        classpath("org.liquibase:liquibase-gradle-plugin:${liquibaseVersion}")
    }
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.liquibase.gradle'


group = 'ee.urgas'
version = '1'
sourceCompatibility = 11
compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "11"
    }
}
compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "11"
    }
}

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
}

dependencies {

    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // Cache
    implementation 'com.github.ben-manes.caffeine:caffeine:3.0.1'

    // Logging
    implementation 'io.github.microutils:kotlin-logging:1.7.10'

    // Database
    implementation 'org.jetbrains.exposed:exposed-core:0.48.0'
    implementation 'org.jetbrains.exposed:exposed-java-time:0.48.0'
    implementation 'org.jetbrains.exposed:exposed-dao:0.48.0'
    implementation 'org.jetbrains.exposed:exposed-jodatime:0.48.0'
    implementation 'org.jetbrains.exposed:exposed-jdbc:0.48.0'
    implementation 'org.jetbrains.exposed:exposed-json:0.48.0'

    implementation 'com.zaxxer:HikariCP:3.4.5'
    implementation 'org.postgresql:postgresql:42.6.0'
    implementation 'org.liquibase:liquibase-core:3.6.3'
    liquibaseRuntime 'org.liquibase:liquibase-core:3.6.3'
    liquibaseRuntime 'org.postgresql:postgresql:42.6.0'

    // Kotlin support
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'

    // Testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    testImplementation 'org.liquibase:liquibase-core:3.7.0'

    // Asciidoc
    implementation 'org.asciidoctor:asciidoctorj:2.4.3'

    // Markdown (CommonMark with GFM extensions)
    implementation 'org.commonmark:commonmark:0.24.0'
    implementation 'org.commonmark:commonmark-ext-gfm-tables:0.24.0'
    implementation 'org.commonmark:commonmark-ext-gfm-strikethrough:0.24.0'

    // Jsoup for Asciidoctor HTML output customisation
    implementation 'org.jsoup:jsoup:1.13.1'

    // StoredFile type detection
    implementation 'org.apache.tika:tika-core:1.25'

    // Source code similarity
    implementation 'me.xdrop:fuzzywuzzy:1.2.0'

    // Temporary JWT parsing for username migration
    implementation 'com.auth0:java-jwt:3.18.3'

    // TSL
    implementation project(":tsl")
}


liquibase {
    activities {
        main {
            changeLogFile 'src/main/resources/db/changelog.xml'
            url liquibaseProperties.url
            username liquibaseProperties.username
            password liquibaseProperties.password
        }
    }
}
